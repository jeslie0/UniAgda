#+title: Identity Types
#+author: James Leslie
#+STARTUP: noindent hideblocks latexpreview
* Prelude
#+begin_src agda2 :tangle yes
{-# OPTIONS --without-K --no-import-sorts --safe #-}
module UniAgda.Core.Types.Identity where

open import UniAgda.Core.Types.Universes
#+end_src
* Identity Types
We define the identity type as an inductive type, and tell Agda that it is the builtin identity.
#+begin_src agda2 :tangle yes
data _≡_ {i : Level} {A : Type i} (x : A) : A → Type i where
  refl : x ≡ x
infix 5 _≡_
_==_ = _≡_
{-# BUILTIN EQUALITY _≡_ #-}
#+end_src

We get both forms of path induction for free.
#+begin_src agda2 :tangle yes
bpath-ind : {i j : Level} {A : Type i}
            (a : A) (C : (x : A) → a ≡ x → Type j) (c : C a refl)
            → (x : A) (p : a ≡ x) → C x p
bpath-ind a C c .a refl = c
#+end_src
#+begin_src agda2 :tangle yes
path-ind : {i j : Level} {A : Type i}
           (C : (x y : A) → x ≡ y → Type j) (c : (x : A) → C x x refl)
         → (a b : A) → (p : a ≡ b) → C a b p
path-ind C c x .x refl = c x
#+end_src
* Groupoid Structure
Path types give rise to a groupoid structure on Types. Reflexivity is proven by the refl map above, symmetry is proven here and corresponds to inverses:
#+NAME: Lemma2.1.1
#+begin_src agda2 :tangle yes
_^ : {i : Level} {A : Type i} {x y : A}
     (p : x ≡ y)
     → y ≡ x
refl ^ = refl
infix 30 _^
#+end_src

Transitivity is proven here and corresponds to concatenation.
#+name: Lemma2.1.2
#+begin_src agda2 :tangle yes
_∘_ : ∀ {i} {A : Type i} {x y z : A}
      (p : x ≡ y) (q : y ≡ z)
      → x ≡ z
_∘_ refl q = q
infixr 20 _∘_
#+end_src

We will prove the groupoid properties in another section.
* Ap and Transport
We have that functions can be applied to paths.
#+name: Lemma2.2.1
#+begin_src agda2 :tangle yes
ap : ∀ {i j} {A : Type i} {B : Type j} {x y : A}
     (f : A → B) (p : x ≡ y)
     → (f x ≡ f y)
ap f refl = refl
#+end_src

Transport gives us an operation on type families.
#+name: Lemma2.3.1
#+begin_src agda2 :tangle yes
transport : ∀ {i j} {A : Type i} {x y : A}
            (P : A → Type j) (p : x ≡ y)
            → P x → P y
transport P refl x = x

syntax transport P p = p #[ P ]
#+end_src

The above give rise to dependant ap.
#+name: Lemma2.3.4
#+begin_src agda2 :tangle yes
apD : ∀ {i j} {A : Type i} {x y : A} {P : A → Type j}
      (f : (x : A) → P x) → (p : x ≡ y)
      → (transport P p (f x)) ≡ (f y)
apD f refl = refl
#+end_src
